///////////////////////////////////////////
////////// IP、天气（RSS）
////////// modified by Cster 2015
///////////////////////////////////////////



TextBetween
{
	_s = _argv[0];
	_begin = _argv[1];
	_end = _argv[2];
	_idx = 0;
	if _argc > 3 {
		_idx = _argv[3];
	}
	_d = _s[_idx + 1,_begin];
	_d = _d[0,_end];
	_d;
}

TextBetween2
{
	_s = _argv[0];
	_begin = _argv[1];
	_end = _argv[2];
	_idx = 0;
	if _argc > 3 {
		_idx = _argv[3];
	}
	_d = _s[_idx,_end];
	_d = _d[1,_begin];
	_d;
}

TextBetween3
{
	_s = _argv[0];
	_begin = _argv[1];
	_end = _argv[2];
	_d = _s[_begin,_end];
	//_d = _d[1,_begin];
	_d;
}




//鼠标拖放文本到伪春菜
/////////////////////////////////////////////////////////

OnTextDrop
{
	if IsIpAdress(reference0) {
		"\![excute,http-post,http://www.ip138.com/ips8.asp,--param=ip=%(reference0)&action=2,--file=checkip.html,--timeout=50,--async=checkip]";
	}
	elseif IsEnglishWord(reference0) {
		_tmp = CUTSPACE(reference0);
		"\![raise,OnWordsInputFinish,%(_tmp)]"
	}
	else {
		reference0
	}
}

IsEnglishWord
{
	RE_MATCH(_argv[0],'\s*\b[a-z|A-Z|\-]+\b\s*');
}


//IP地址查询，查询自己外网IP
////////////////////////////////////////////////////////////
OnMyIp
{
	"查询中，请稍候……\![excute,http-get,http://www.ip138.com/ip2city.asp,--file=ip.html,--timeout=50,--async=myip]";
}

OnMyIpFound
{
	_txt = "";
	_file = "var\\ip.html";
	_buff = "";
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			_txt += _buff;
		}
		_txt = _txt[1,'['];
		_txt = _txt[0,']'];
	}
	FCLOSE(_file);
	_txt;
}


OnCheckIp
{
	"请输入你要查询的IP地址：\![open,inputbox,OnCheckIpComplete,-1]";
}

OnCheckIpComplete
{
	if IsIpAdress(reference0) {
		"\![excute,http-post,http://www.ip138.com/ips8.asp,--param=ip=%(reference0)&action=2,--file=checkip.html,--timeout=50,--async=checkip]";
	}
	else {
		"不是正确的IPv4地址……\n";
	}
}

IsIpAdress
{
	_ip = REPLACE(_argv[0],'.',',');
	_count = 0;
	_outofrange = 0;
	foreach _ip; _i {
		if (TOINT(_i) > 255 || TOINT(_i) < 0) && _i != '*' {
			_outofrange = 1;
			break;
		}
		_count ++;
	}
	if _count == 4 && _outofrange == 0 {
		1
	}
	else {
		0
	}
}

OnIpFound
{
	_txt = "\_q";
	_file = "var\\checkip.html";
	_buff = "";
	FCHARSET(127);
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '您查询的IP' _in_ _buff {
				for _buff += FREAD(_file); '参考数据二' !_in_ _buff; _buff += FREAD(_file) {
				}
				_ip = TextBetween(_buff,'您查询的IP:','<');
				_info1 = TextBetween(_buff,'本站主数据','<');
				_info2 = TextBetween(_buff,'参考数据一','<');
				_info3 = TextBetween(_buff,'参考数据二','<');
				_txt += "\f[bold,1]您查询的IP：%(_ip)\f[bold,default]\n本站主数据%(_info1)\n参考数据一%(_info2)\n参考数据二%(_info3)\n";
				break;
			}
		}
	}
	FCLOSE(_file);
	FCHARSET(1);
	_txt += "\n[50]数据来自\_a[OnUrl,Http://www.ip138.com/]www.ip138.com\_a\n/
			\n\q[◇返回,OnOpenMenu]    \q[◇终了,OnIpEnd]";
	_txt;
}

OnIpEnd
{
	"\0\s[41]那个……橘花有帮到你吗？\w9";
}


//天气查询
////////////////////////////////////////////////////////////////////////////////

OnCheckWeather
{
	if ISVAR("city") {
		"\b[1]要查询『\_a[OnChangeCity]%(city)\_a』的天气吗？/
		\n[150]\_q\__q[OnDownLoadWeather]◇新浪天气查询\__q\n/
		（未来3-5天天气状况，可查询国外城市）\n/
		\_a[OnUrl,http://weather.news.sina.com.cn/]http://weather.news.sina.com.cn/\_a\n[150]";
		--
		"\q[◇返回,OnOpenMenu]    \q[◇取消,OnWeatherEnd]\_q";
	} 
	else {
		OnChangeCity;
	}
}

OnWeatherEnd
{

}

OnChangeCity
{
	"请输入你要查询的城市名称：\![open,inputbox,OnChangeCityComplete,-1,%(city)]";
}

OnChangeCityComplete
{
	city = reference0;
	OnCheckWeather;

}


OnDownLoadWeather
{
	"正在连接网站，请稍等……\![excute,http-post,http://php.weather.sina.com.cn/search.php,--param=city=%(city),--param-charset=UTF-8,--file=weather-sina.html,--timeout=200,--async=weathersina]";
}

OnDownLoadWeather2
{
	"正在连接网站，请稍等……\![excute,http-get,http://www.google.cn/search?hl=UTF-8&q=天气+%(city),--file=weatherig.html,--timeout=200,--async=weatherig]";
}

OnWeatherFound
{
        _txt = "\![set,autoscroll,disable]\![set,choicetimeout,0]\_q\b[2]『\_a[OnChangeCity]%(city)\_a』天气预报：\n";
	_file = "var\\weather-sina.html";
	_buff = "";
	_today = "";
	_found = 0;
	FCHARSET(127);
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '<div class="tab_hd clearfix">' _in_ _buff {
                                _buff = FREAD(_file);
				_updatetime = TextBetween(_buff,'<h3>','</h3>'); 
                                _updatetime = REPLACE(_updatetime,'<span style="font-size:12px;','');
                                _updatetime = REPLACE(_updatetime,'</span>','');
                                _updatetime = REPLACE(_updatetime,'">','');


				_txt += _updatetime + "\n";
			}
			/*if 'alt="当地时间"' _in_ _buff || 'alt="今天"' _in_ _buff {
				_today = TextBetween(_buff,'年','</span>');
				_today = TextBetween(_buff,'alt="','"') + "  " + _today[1,' '] + "(" + _today[0,' '] + ")";
			}*/
			if '<div class="mod_today clearfix">' _in_ _buff {
				_found = 1;
				for _buff = FREAD(_file); '<div class="weather_list clearfix">' !_in_ _buff; _buff += FREAD(_file) {
				}
				//_txt += "\f[bold,1]%(_today)\f[bold,0]\n";
				_txt += "　　　" + TextBetween(_buff,'<h5>','</h5>',0) + "\_l[150]" + TextBetween(_buff,'<h5>','</h5>',1) + "\n";
				_txt += "天气：" + TextBetween(_buff,'<li>','</li>',1) + "\_l[150]" + TextBetween(_buff,'<li>','</li>',4) + "\n";
				_txt += "气温：" + TextBetween(_buff,'<span class="fs_30 tpte">','</span>',0) + "  " + TextBetween(_buff,'<span class="fs_14">','</span>',0) + TextBetween(_buff,'<span class="fs_24 tpte">','</span>',0) + "\_l[150]" + TextBetween(_buff,'<span class="fs_30 tpte">','</span>',1) + "\n";
                                _txt += "风力：" + TextBetween(_buff,'<li>','</li>',2) + "\_l[150]" + TextBetween(_buff,'<li>','</li>',5) + "\n";
                                //_txt += "天气：" + TextBetween(_buff,'天气：','风力') + "\n";
                               // _txt += "风力：" + TextBetween(_buff,'风力：','风向') + "\n";
				//_txt += "风向：" + TextBetween(_buff,'风向：',')') + "\n";
				
			}




			if '<div class="weather_list clearfix">' _in_ _buff {
				_days = 0;
				for _tmp = FREAD(_file); '<ul class="list_01">' !_in_ _tmp; _tmp = FREAD(_file) {
					_buff += _tmp;
					if '<div class="mod_02">' _in_ _tmp {
						_days ++;
					}
				}
				for _i = 0; _i < _days; _i ++ {
					_table = TextBetween(_buff,'<div class="mod_02">','<div class="mod_02">',_i);
					//_table = REPLACE(_table,'&nbsp;','暂无');
					//_txt += "\f[bold,1]" + TextBetween(_buff,'scope="col">','<',_i) + "\f[bold,0]\n";
					_txt += "　　　" + TextBetween(_table,'<h4>','</h4>',0) + "\n";
					_txt += "\_l[36]" + "白天" + "\_l[150]" + "夜间" + "\n";
                                        _txt += "天气：" + TextBetween(_table,'<li>','</li>',0) + "\_l[150]" + TextBetween(_table,'<li>','</li>',3) + "\n";
					_txt += "气温：" + TextBetween(_table,'<li class="tpte">','</li>',0) + "\_l[150]" + TextBetween(_table,'<li class="tpte">','</li>',1) + "\n";
					_txt += "风向：" + TextBetween(_table,'<li>','</li>',1) + "\_l[150]" + TextBetween(_table,'<li>','</li>',4) + "\n";
                                        _txt += "风力：" + TextBetween(_table,'<li>','</li>',2) + "\_l[150]" + TextBetween(_table,'<li>','</li>',5) + "\n";

				}
			}
	
              }
	}
	if _found == 0 {
		_txt += "对不起，您搜索的城市天气信息不存在，请改变后重新尝试！\n"
	}
	FCLOSE(_file);
	FCHARSET(1);
	_txt += "\n\q[◇返回,OnCheckWeather]    \q[◇终了,OnWeatherEnd]\e"
	_txt;
}

OnWeatherFound2
{
	_txt = "\b[2]『\_a[OnChangeCity]%(city)\_a』未来3天天气预报：\n";
	_file = "var\\weatherig.html";
	_buff = "";
	_found = 0;
	FCHARSET(127);
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '天气' _in_ _buff && '添加到 iGoogle' _in_ _buff {
				_title = TextBetween(_buff,'<FONT color=#cc0033>','<');
				_tmp = TextBetween(_buff,'添加到 iGoogle','搜索结果');
				_wind = TextBetween(_tmp,'>','<',11);
				_wet = TextBetween(_tmp,'>','<',12);
				_info = TextBetween(_tmp,'color:#6f6f6f">','<');
				_found = 1;
				_txt += "（%(_info)）\n\n";
				for _i = 0; _i < 3; _i++ {
					_day = TextBetween(_tmp,'float:left">','<',_i + 1);
					_weather = TextBetween(_tmp,'alt="','"',_i);
					_temperature = TextBetween(_tmp,'<nobr>','<',_i);
					_txt += "\f[bold,1]%(_day)\f[bold,default]\n天气：%(_weather)\n气温：%(_temperature)\n";
					if _day == '今日' {
						_txt += "%(_wind)\n%(_wet)\n[150]";
					}
					else {
						_txt += "\n[half]";
					}
				}

			}
		}
	}
	FCLOSE(_file);
	FCHARSET(1);
	if _found == 0 {
		_txt += "\n没有查到这个城市的天气情况……\n";
	}
	_txt += "\n\q[◇返回,OnCheckWeather]    \q[◇终了,OnWeatherEnd]\e"
	_txt;	
}



//Google计算器
//////////////////////////////////////////////////////////////////////
OnGoogleCal
{
	"\b[2]\![set,autoscroll,false]请输入你要计算的表达式：\n[150]\![open,inputbox,OnGoogleCalInput,-1]/
	\_qTips：\n/
	\_l[0]符号\_l[76]功能\_l[160]示例\n/
	\_l[0]+\_l[76]加\_l[160]3+44\n/
	\_l[0]-\_l[76]减\_l[160]13-5\n/
	\_l[0]*\_l[76]乘\_l[160]7*8\n/
	\_l[0]/\_l[76]除以\_l[160]12/3\n/
	\_l[0]^\_l[76]的…次方\_l[160]8^2\n/
	\_l[0]\%\_l[76]模\_l[160]8%7\n/
	\_l[0]选\_l[76]组合运算\_l[160]18选4\n/
	\_l[0]开…次方\_l[76]n次方根\_l[160]32开5次方\n/
	\_l[0]sqrt\_l[76]平方根\_l[160]sqrt(9)\n/
	\_l[0]sin,cos,etc.\_l[76]三角函数(弧度)\_l[160]sin(pi/3)\n/
	\_l[0]ln\_l[76]e底对数\_l[160]ln(17)\n/
	\_l[0]log\_l[76]10底对数\_l[160]log(1000)\n/
	\_l[0]!\_l[76]阶乘\_l[160]5!\n/
	\_l[0]\%\_l[76]百分之\_l[160]20\%\n/
	\n/
	你可以将等号（=）放在一个式子之后对这个式子进行强制计算。如果这个式子是合理的，Google将返回结果。\n[150]/
	你也可以用十六进制，八进制，或者二进制数进行计算，只需要在十六进制数前加前缀 0x ，在八进制数前加前缀 0o ，在二进制数前加前缀 0b 。\n[150]/
	Google计算器能识别很多单位，以及各种物理和数学常数。你也可以在计算表达式中直接用这些常数。你可以用是几，等于多少，或者＝？来做单位间的换算。比如，三十摄氏度等于多少华氏度。\n/
	\_q\x";
}

OnGoogleCalInput
{
	_t = reference0;
	_in = ('%','#','&','+','=','?',' ');
	_out = ('%25','%23','%26','%2B','%3D','%3F','+');
	for _i = 0; _i < 7; _i ++ {
		_t = REPLACE(_t,_in[_i],_out[_i]);
	}
//	_t = STRENCODE(_t,"base64");
	"正在连接Google……\![excute,http-get,http://www.google.cn/search,--param=hl=zh-CN&q=%(_t),--file=googlecal.html,--timeout=50,--async=googlecal]";
}


OnGoogleCalFound
{
	_txt = "\_q";
	_file = "var\\googlecal.html";
	_buff = "";
	_found = 0;
	FCHARSET(127);
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '计算器详情' _in_ _buff || '汇率比价仅作信息参考' _in_ _buff {
				_tmp = _buff[1,'font-size:138'];
				_tmp = _tmp[0,'</b>'];
				_tmp = _tmp[1,'<b>'];
				_tmp = REPLACE(_tmp,'&#160;',' ');
				_txt += "您要计算的表达式：\n[150]\f[bold,1]%(_tmp)\f[bold,default]\n";
				_found = 1;
				break;
			}
		}
	}
	FCLOSE(_file);
	FCHARSET(1);
	if _found == 0 {
		_txt += "Google无法计算您给的表达式，请检查输入是否正确……";
	}
	_txt += "\n\n\q[◇返回,OnOpenMenu]    \q[◇终了,OnGoogleCalEnd]";
	_txt;
}

OnGoogleCalEnd
{
	"\0\s[41]那个……橘花有帮到你吗？\w9";
}


//RSS读取
//////////////////////////////////////////////////////////////////////

/*

GetHeadlineList
{
	_txt = "";
	_count = 0;
	_list = IARRAY;
	_path = FENUM("..\\..\\..\\..\\headline");
	_path = REPLACE(_path,'\','\\');
	foreach _path; _i {
		_list[_count] = '..\\..\\..\\..\\headline' + _i + '\\';
		_count ++;
	}
	TOSTR(_list);
}


ReadXml
{
	_file = _argv[0];
	_buff = "";
	_txt = "\_q\b[2]";
	_title = "";
	_url = "";
	if FOPEN(_file,'r') {
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) {
			if '<item' _in_ _buff {
				_title = "";
				_url = "";
				_endofitem = 0;
				while _endofitem != 1 {
					if '<title>' _in_ _buff {
						while '</title>' !_in_ _buff {
							_buff += FREAD(_file);
						}
						_tmp = _buff[1,'<title>'];
						_tmp = _tmp[0,'</title>'];
						_title = _tmp;
					}
					if '<link>' _in_ _buff {
						while '</link>' !_in_ _buff {
							_buff += FREAD(_file);
						}
						_tmp = _buff[1,'<link>'];
						_tmp = _tmp[0,'</link>'];
						_url = _tmp;
					}
					if '</item>' _in_ _buff {
						_endofitem = 1;
						if _url != "" {
							_txt += '\_a[OnUrl,' + _url + ']' + _title + '\_a\n';
						}
						else {
							_txt += _title + '\n';
						}
					}
					else {
						_buff = FREAD(_file);
					}
				}
			}
		}
	}
	FCLOSE(_file);
	_txt = REPLACE(_txt,'&quot;','"');
	_txt = REPLACE(_txt,'&amp;','&');
	_txt = REPLACE(_txt,'&nbsp;',' ');
	_txt = REPLACE(_txt,'&lt;','<');
	_txt = REPLACE(_txt,'&gt;','>');
	_txt = REPLACE(_txt,'<br />',' ');
	_txt;
}


SendTweet
{
	_username = "forjanezz";
	_password = "111111";
	_message = "http://aaa.bbb.ccc";
	_url = "http://%(_username):%(_password)@twitter.com/statuses/update.xml"
	_txt = "";
	_txt += "\_q%(_url)\![execute,http-post,%(_url),--param=status=%(_message),--param-charset=UTF-8,--file=twitter.xml,--timeout=200,--async=twitterpost]";
	_txt;
}

*/






























/////////////////////////////////////////////////////////////////////////////

OnUrl
{
	"\j[%(reference0)]";
}

OnExecuteHTTPComplete
{
	if reference1 == 'kikkagetip' {
		OpenMenu(0);
	}
	if reference1 == 'infogetip' {
		Select.System;
	}
	elseif reference1 == 'myip' {
		OnMyIpFound;
	}
	elseif reference1[0,'='] == 'rss' {
		_tmp = reference1[1,'='];
//		ReadXml("var\\%(_tmp)");
	}
	elseif reference1 == 'weathersina' {
		OnWeatherFound;
	}

	elseif reference1 == 'weatherig' {
		OnWeatherFound2;
	}
	elseif reference1 == 'checkip' {
		OnIpFound;
	}
	elseif reference1 == 'googlecal' {
		OnGoogleCalFound;
	}
	else {
		"\0\s[3]？\e";
	}
}

OnExecuteHTTPFailure
{
	if reference1 == 'kikkagetip' {
		if "ip.php" _in_ FENUM("var") {
			_tmp = FDEL("var/ip.php");
		}
		"\0\b[2]\_q\_l[147,302]连线失败……\_q\_l[0,0]";
		--
		OpenMenu(0);
	}
	elseif reference1 == 'weatherurl' || reference1 == 'weather' || reference1 == 'weather24' || reference1 == 'weatherig' {
		"\0\s[3]连线失败了……\nError：%(reference4)\n要查看本地信息吗？\n\q[确定,OnWeatherFound]"
		--
		"\n\n\q[◇返回,OnCheckWeather]    \q[◇终了,OnWeatherEnd]\e"
	}
	else {
		"\0\s[3]连线失败了……\nError：%(reference4)\n要查看本地信息吗？\n\q[确定,OnWeatherFound]\e"
	}
}